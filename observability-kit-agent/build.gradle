plugins {
  id "java"
  id 'maven-publish'

  /*
  Instrumentation agent extension mechanism expects a single jar containing everything required
  for your extension. This also includes any external libraries that your extension uses and
  cannot access from application classpath (see comment below about `jakarta.servlet-api` dependency).

  Thus we use Shadow Gradle plugin to package our classes and all required runtime dependencies
  into a single jar.
  See https://imperceptiblethoughts.com/shadow/ for more details about Shadow plugin.
   */
  id "com.github.johnrengelman.shadow" version "7.1.2"
  id "com.diffplug.spotless" version "6.9.1"

  id "io.opentelemetry.instrumentation.muzzle-generation" version "1.17.0-alpha-SNAPSHOT"
  id "io.opentelemetry.instrumentation.muzzle-check" version "1.17.0-alpha-SNAPSHOT"
}

group 'com.vaadin.observability'
version project.getProperty('projectVersion')

ext {
  versions = [
    bytebuddy: "1.12.13",
    junit: "5.6.2",
    opentelemetry              : "1.21.0",
    opentelemetryAlpha         : "1.21.0-alpha",
    opentelemetryJavaagent     : "1.21.0",
    opentelemetryJavaagentAlpha: "1.21.0-alpha",
    logbackClassic: "1.2.9",
    mockito: "4.7.0",
    servletApi: "6.0.0",
  ]

  deps = [
    autoservice: dependencies.create(group: 'com.google.auto.service', name: 'auto-service', version: '1.0')
  ]
}

repositories {
  mavenCentral()
  maven { url = 'https://maven.vaadin.com/vaadin-prereleases' }
  maven {
    name = "vaadin-prereleases"
    url = uri("https://maven.vaadin.com/vaadin-prereleases/")
  }
  maven {
    name = "sonatype"
    url = uri("https://oss.sonatype.org/content/repositories/snapshots")
  }
}

configurations {
  /*
  We create a separate gradle configuration to grab a published Otel instrumentation agent.
  We don't need the agent during development of this extension module.
  This agent is used only during integration test.
  */
  otel
}


dependencies {
//  compileOnly("com.google.auto.value:auto-value-annotations:1.9")
//  annotationProcessor("com.google.auto.value:auto-value:1.9")

  /*
  Interfaces and SPIs that we implement. We use `compileOnly` dependency because during
  runtime all necessary classes are provided by javaagent itself.
   */
  compileOnly("io.opentelemetry:opentelemetry-sdk-extension-autoconfigure-spi:${versions.opentelemetry}")
  compileOnly("io.opentelemetry.instrumentation:opentelemetry-instrumentation-api:${versions.opentelemetryJavaagentAlpha}")
  compileOnly("io.opentelemetry.instrumentation:opentelemetry-instrumentation-api-semconv:${versions.opentelemetryJavaagentAlpha}")
  compileOnly("io.opentelemetry.javaagent:opentelemetry-javaagent-extension-api:${versions.opentelemetryJavaagentAlpha}")

  compileOnly("com.vaadin:flow-server:${flowVersion}")
  compileOnly("com.vaadin:flow-data:${flowVersion}")
  implementation('com.vaadin:license-checker:1.11.2')

  //Provides @AutoService annotation that makes registration of our SPI implementations much easier
  compileOnly deps.autoservice
  annotationProcessor deps.autoservice

  /*
   Used by our demo instrumentation module to reference classes of the target instrumented library.
   We again use `compileOnly` here because during runtime these classes are provided by the
   actual application that we instrument.

   NB! Only Advice (and "helper") classes of instrumentation modules can access classes from application classpath.
   See https://github.com/open-telemetry/opentelemetry-java-instrumentation/blob/main/docs/contributing/writing-instrumentation-module.md#advice-classes
   */
  compileOnly group: 'jakarta.servlet', name: 'jakarta.servlet-api', version: versions.servletApi

  /*
  This dependency is required for DemoSpanProcessor both during compile and runtime.
  Only dependencies added to `implementation` configuration will be picked up by Shadow plugin
  and added to the resulting jar for our extension's distribution.
   */
  implementation 'org.apache.commons:commons-lang3:3.11'

  //All dependencies below are only for tests
  testImplementation("com.vaadin:flow-server:${flowVersion}")
  testImplementation("com.vaadin:flow-data:${flowVersion}")
  testImplementation("io.opentelemetry.instrumentation:opentelemetry-instrumentation-api-semconv:${versions.opentelemetryJavaagentAlpha}")
  testImplementation("io.opentelemetry.javaagent:opentelemetry-javaagent-extension-api:${versions.opentelemetryJavaagentAlpha}")
  testImplementation("org.mockito:mockito-inline:${versions.mockito}")
  testImplementation("net.bytebuddy:byte-buddy:${versions.bytebuddy}")
  testImplementation("net.bytebuddy:byte-buddy-agent:${versions.bytebuddy}")
  testImplementation("jakarta.servlet:jakarta.servlet-api:${versions.servletApi}")

  testImplementation("org.junit.jupiter:junit-jupiter-api:${versions.junit}")
  testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${versions.junit}")
  testRuntimeOnly("ch.qos.logback:logback-classic:${versions.logbackClassic}")

  //Otel Java instrumentation that we use and extend during integration tests
  otel("io.opentelemetry.javaagent:opentelemetry-javaagent:${versions.opentelemetryJavaagent}")

  //TODO remove when start using io.opentelemetry.instrumentation.javaagent-instrumentation plugin
  add("codegen", "io.opentelemetry.javaagent:opentelemetry-javaagent-tooling:${versions.opentelemetryJavaagentAlpha}")
  add("codegen", "ch.qos.logback:logback-classic:${versions.logbackClassic}")
  add("muzzleBootstrap", "io.opentelemetry.instrumentation:opentelemetry-instrumentation-api-annotation-support:${versions.opentelemetryJavaagentAlpha}")
  add("muzzleTooling", "io.opentelemetry.javaagent:opentelemetry-javaagent-extension-api:${versions.opentelemetryJavaagentAlpha}")
  add("muzzleTooling", "io.opentelemetry.javaagent:opentelemetry-javaagent-tooling:${versions.opentelemetryJavaagentAlpha}")
}

//Produces a copy of upstream javaagent with this extension jar included inside it
//The location of extension directory inside agent jar is hard-coded in the agent source code
task extendedAgent(type: Jar) {
  dependsOn(configurations.otel)
  archiveFileName = "vaadin-opentelemetry-javaagent-${projectVersion}.jar"
  from zipTree(configurations.otel.singleFile)
  from(tasks.shadowJar.archiveFile) {
    into "extensions"
  }

  //Preserve MANIFEST.MF file from the upstream javaagent
  doFirst {

    def lines = zipTree(configurations.otel.singleFile).matching { include 'META-INF/MANIFEST.MF' }.singleFile.readLines()
    for(int i = 0;i < lines.size(); i++) {
      // Connect values that are split to multiple lines
      if(!lines[i].contains(':')) {
        lines[i-1] = lines[i-1].trim() + lines[i].trim();
        lines.remove(i);
        i--;
      }
      // Replace version line
      if(lines[i].startsWith("Implementation-Version")) {
        lines[i] = "Implementation-Version:${versions.opentelemetryJavaagent} - vaadin ${projectVersion}"
      }
    }
    manifest.attributes(
            lines.collectEntries(l -> [
              l.split(':')[0], l.split(':')[1]
            ])
    )
  }
}

// Copy sources and replace the 2 placeholders on copy
task sources(type: Copy) {
  from 'src/main/java'
  into 'build/adjustedSrc'

    filter { String line -> line.replace('@INSTRUMENTATION_NAME@', "com.vaadin.observability.instrumentation")}
    filter { String line -> line.replace('@INSTRUMENTATION_VERSION@', "${projectVersion}")}
}

// Prepare sources, before compile, compile adjusted sources
compileJava {
  dependsOn sources
  source = sources.destinationDir
  classpath = sourceSets.main.compileClasspath
}

tasks {

  compileJava {
    options.release.set(17)
  }

  assemble.dependsOn(shadowJar)
}

test {
  useJUnitPlatform()
}

muzzle {
  pass {
    group.set("jakarta.servlet")
    module.set("jakarta.servlet-api")
    versions.set("[5.0.0,)")
    assertInverse.set(true)
  }
}

spotless {
  java {
    removeUnusedImports()
    eclipse().configFile('eclipse/VaadinJavaConventions.xml')
    importOrder('\\#', 'elemental', 'com.vaadin', 'com.google.gwt', '', 'java', 'jakarta')
  }
}
sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17


publishing {
  publications {
    maven(MavenPublication) {
      artifactId = 'vaadin-opentelemetry-javaagent'
      artifact ("${jar.destinationDirectory}/vaadin-opentelemetry-javaagent-${projectVersion}.jar")
      pom {
        name = 'Vaadin Observability Kit'
        description = 'Observability for vaadin'
        url = 'http://www.vaadin.com/'
        licenses {
          license {
            name = 'Commercial Vaadin Developer License 4.0 (CVDLv4)'
            url = 'https://vaadin.com/license/cvdl-4.0'
          }
        }
      }
    }
  }
  repositories {
    maven {
      url = "${mavenUrl}"
      credentials {
        username = "${mavenUser}"
        password = "${mavenPwd}"
      }
    }
  }
}
